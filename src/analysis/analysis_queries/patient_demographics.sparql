BASE <http://purl.regenstrief.org/NDPBRN/dental-practice/>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX owl: <http://www.w3.org/2002/07/owl#>
PREFIX obo: <http://purl.obolibrary.org/obo/>
PREFIX gender_role: <http://purl.obolibrary.org/obo/OMRSE_00000007>
PREFIX inheres_in: <http://purl.obolibrary.org/obo/BFO_0000052>
PREFIX birth_date: <http://purl.obolibrary.org/obo/OHD_0000050>
PREFIX first_visit_date: <http://purl.obolibrary.org/obo/OHD_0000218>
PREFIX last_visit_date: <http://purl.obolibrary.org/obo/OHD_0000219>
PREFIX pbrn_id: <http://purl.obolibrary.org/obo/OHD_0000273>
PREFIX sesame: <http://www.openrdf.org/schema/sesame#>
PREFIX patient: <http://purl.obolibrary.org/obo/OHD_0000012>

select distinct ?practice ?patient_id ?gender ?dob ?first_visit ?last_visit
where {
    ## find instance of patient and patient's DOB
    ?patient_i 
        a patient:;
        birth_date: ?dob;
        pbrn_id: ?pbrn_id .
    
    ## find patient's gender
    ?gender_t 
        rdfs:subClassOf gender_role:;
        rdfs:label ?gender_name .
    ?gender_i 
        sesame:directType ?gender_t;
        inheres_in: ?patient_i .
    
    ## find first and last visit of patient
    ?patient_i 
        first_visit_date: ?first_visit;
        last_visit_date: ?last_visit .
    bind(strafter(str(?pbrn_id), "NDPBRN practice ") as ?practice)
    bind(strafter(str(?patient_i), "patient/") as ?patient_id)
    bind(strbefore(str(?gender_name), " ") as ?gender)    
}
#limit 100