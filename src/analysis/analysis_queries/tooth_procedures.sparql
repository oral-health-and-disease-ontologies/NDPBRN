BASE <http://purl.regenstrief.org/NDPBRN/dental-practice/>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX owl: <http://www.w3.org/2002/07/owl#>
PREFIX obo: <http://purl.obolibrary.org/obo/>
PREFIX pbrn_id: <http://purl.obolibrary.org/obo/OHD_0000273>
PREFIX sesame: <http://www.openrdf.org/schema/sesame#>
PREFIX patient: <http://purl.obolibrary.org/obo/OHD_0000012>
PREFIX tooth: <http://purl.obolibrary.org/obo/FMA_12516>
PREFIX dental_procedure: <http://purl.obolibrary.org/obo/OHD_0000002>
PREFIX extraction_procedure: <http://purl.obolibrary.org/obo/OHD_0000057>
PREFIX procedure_finding: <http://purl.obolibrary.org/obo/OHD_0000221>
PREFIX ada_num: <http://purl.obolibrary.org/obo/OHD_0000065>
PREFIX part_of: <http://purl.obolibrary.org/obo/BFO_0000050>
PREFIX  has_output: <http://purl.obolibrary.org/obo/OBI_0000299>
PREFIX  occurrence_date: <http://purl.obolibrary.org/obo/OHD_0000015>
PREFIX is_about: <http://purl.obolibrary.org/obo/IAO_0000136>
PREFIX prop: <http://purl.regenstrief.org/NDPBRN/property/>

select distinct ?practice ?patient_id ?tooth_id ?tooth_num ?ada_code ?event_name ?event_date ?event_id
where {
    ## find instance of patient
    ?patient_i 
        a patient:;
        pbrn_id: ?pbrn_id .
    
    ## find patients' teeth
    ?tooth_i
        a tooth:;
        prop:ada_tooth_number ?tooth_num;
        part_of: ?patient_i .
    
    # find dental procedures performed on the patient's teeth
    ?event_t 
        rdfs:subClassOf dental_procedure:;
        rdfs:label ?event_name .
    ?event_i 
        sesame:directType ?event_t;
        has_output: ?tooth_i;
        occurrence_date: ?event_date;
        prop:ada_code ?ada_code .
    
    # remove procedures that are existing services
    filter not exists {
        ?procedure_finding 
            a procedure_finding:;
            is_about: ?event_i .
    }
    
    bind(strafter(str(?pbrn_id), "NDPBRN practice ") as ?practice)
    bind(strafter(str(?patient_i), "patient/") as ?patient_id)
    bind(strafter(str(?tooth_i), "tooth/") as ?tooth_id)
    bind(strafter(str(?event_i), "http://purl.regenstrief.org/NDPBRN/dental-practice/") as ?event_id)
}
#limit 100