BASE <http://purl.regenstrief.org/NDPBRN/dental-practice/>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX owl: <http://www.w3.org/2002/07/owl#>
PREFIX obo: <http://purl.obolibrary.org/obo/>
PREFIX pbrn_id: <http://purl.obolibrary.org/obo/OHD_0000273>
PREFIX sesame: <http://www.openrdf.org/schema/sesame#>
PREFIX patient: <http://purl.obolibrary.org/obo/OHD_0000012>
PREFIX tooth: <http://purl.obolibrary.org/obo/FMA_12516>
PREFIX surface: <http://purl.obolibrary.org/obo/FMA_no_fmaid_Surface_enamel_of_tooth>
PREFIX mesial_surface: <http://purl.obolibrary.org/obo/FMA_no_fmaid_Mesial_surface_enamel_of_tooth>
PREFIX occlusal_surface: <http://purl.obolibrary.org/obo/FMA_no_fmaid_Occlusal_surface_enamel_of_tooth>
PREFIX distal_surface: <http://purl.obolibrary.org/obo/FMA_no_fmaid_Distal_surface_enamel_of_tooth>
PREFIX buccal_surface: <http://purl.obolibrary.org/obo/FMA_no_fmaid_Buccal_surface_enamel_of_tooth>
PREFIX labial_surface: <http://purl.obolibrary.org/obo/FMA_no_fmaid_Labial_surface_enamel_of_tooth>
PREFIX lingual_surface: <http://purl.obolibrary.org/obo/FMA_no_fmaid_Lingual_surface_enamel_of_tooth>
PREFIX incisal_surface: <http://purl.obolibrary.org/obo/FMA_no_fmaid_Incisal_surface_enamel_of_tooth>
PREFIX caries_finding: <http://purl.obolibrary.org/obo/OHD_0000024>
PREFIX lesion: <http://purl.obolibrary.org/obo/OHD_0000021> 
PREFIX dental_procedure: <http://purl.obolibrary.org/obo/OHD_0000002>
PREFIX ada_num: <http://purl.obolibrary.org/obo/OHD_0000065>
PREFIX part_of: <http://purl.obolibrary.org/obo/BFO_0000050>
PREFIX occurrence_date: <http://purl.obolibrary.org/obo/OHD_0000015>
PREFIX is_about: <http://purl.obolibrary.org/obo/IAO_0000136>
PREFIX prop: <http://purl.regenstrief.org/NDPBRN/property/>

select distinct ?tooth_id ?tooth_num ?m ?o ?d ?b ?l ?f ?i ?event_id
where {
    ## find instance of patient
    ?patient_i 
        a patient:;
        pbrn_id: ?pbrn_id .
    
    ## find patients' teeth
    ?tooth_i
        a tooth:;
        prop:ada_tooth_number ?tooth_num;
        part_of: ?patient_i .
   
    ## find surface that is part of tooth
#    ?surface_i
#        a surface:;
#        part_of: ?tooth_i;
#        prop:surface_letter ?surface .
   
    ## findings about lesions on tooth
    ?event_t 
        rdfs:subClassOf caries_finding:; 
        rdfs:label ?event_name .
    ?event_i
        sesame:directType ?event_t;
        occurrence_date: ?event_date;
        is_about: [a lesion:; part_of: ?tooth_i] .
#        is_about: [a lesion:; part_of: ?surface_i] . # test
    
    ## find which surfaces the finding is about
    optional {
        ?event_i 
            is_about: ?lesion_m . # mesial lesial
        ?lesion_m 
            a lesion:; 
            part_of: ?mesial . 
        ?mesial 
            sesame:directType mesial_surface:;
            prop:surface_letter ?surface_m;
            part_of: ?tooth_i .
    } 
    optional {
        ?event_i 
            is_about: ?lesion_o . #occlusal lesion
        ?lesion_o 
            a lesion:; 
            part_of: ?occlusal .
        ?occlusal 
            sesame:directType occlusal_surface:; 
            prop:surface_letter ?surface_o;
            part_of: ?tooth_i . 
    } 
    optional {
        ?event_i 
            is_about: ?lesion_d . # distal lesion
        ?lesion_d 
            a lesion:; 
            part_of: ?distal .
        ?distal 
            sesame:directType distal_surface:; 
            prop:surface_letter ?surface_d;
            part_of: ?tooth_i .
    } 
    optional {
        ?event_i 
            is_about: ?lesion_b . # buccal lesion
        ?lesion_b 
            a lesion:; 
            part_of: ?buccal .
        ?buccal 
            sesame:directType buccal_surface:; 
            prop:surface_letter ?surface_b;
            part_of: ?tooth_i .
    } 
    optional {
        ?event_i 
			is_about: ?lesion_l . # lingual lesion
        ?lesion_l 
            a lesion:; 
            part_of: ?lingual .
        ?lingual
            sesame:directType lingual_surface:; 
            prop:surface_letter ?surface_l;
            part_of: ?tooth_i .
    } 
    optional {
        ?event_i 
            is_about: ?lesion_f . # facial/labial lesion
        ?lesion_f 
            a lesion:; 
            part_of: ?facial .
        ?facial 
            sesame:directType labial_surface:; 
            prop:surface_letter ?surface_f;
            part_of: ?tooth_i .
    } 
    optional {
        ?event_i 
            is_about: ?lesion_incisal . # incisal lesion
        ?lesion_incisal 
            a lesion:; 
            part_of: ?incisal .
        ?incisal 
            sesame:directType incisal_surface:; 
            prop:surface_letter ?surface_incisal;
            part_of: ?tooth_i .
    }
 
    bind(strafter(str(?pbrn_id), "NDPBRN practice ") as ?practice)
    bind(strafter(str(?patient_i), "patient/") as ?patient_id)
    bind(strafter(str(?tooth_i), "tooth/") as ?tooth_id)
    bind(strafter(str(?event_i), "http://purl.regenstrief.org/NDPBRN/dental-practice/") as ?event_id)
    
    # flags for surfaces
    bind(if(bound(?surface_m), 1, 0) as ?m) # mesial
    bind(if(bound(?occlusal), 1, 0) as ?o) # occlusal
    bind(if(bound(?distal), 1, 0) as ?d) # distal
    bind(if(bound(?buccal), 1, 0) as ?b) # buccal
    bind(if(bound(?lingual), 1, 0) as ?f) # lingual
    bind(if(bound(?facial), 1, 0) as ?l) # facial
    bind(if(bound(?incisal), 1, 0) as ?i) # incisal
}
# limit 100