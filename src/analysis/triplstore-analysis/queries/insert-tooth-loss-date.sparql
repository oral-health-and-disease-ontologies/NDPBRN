## name: insert-tooth-loss-date.sparql
## author: Bill Duncan
## purpose:
##   This query inserts the date on which a given tooth lost.
##   This could be due to an extraction or the result of a finding.
##   It helps speed up access to this information by simply querying for: 
##     ?tooth_i prop:tooth_loss_date ?loss_date

## NB: This query depends on running previously running:
## 	* insert-tooth-instance-tooth-num.sparql first
##  * insert-missing-tooth-finding-date.sparql
##  * insert-tooth-extraction-date.sparql

PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX part_of: <http://purl.obolibrary.org/obo/BFO_0000050>
PREFIX has_part: <http://purl.obolibrary.org/obo/BFO_0000051>
PREFIX tooth: <http://purl.obolibrary.org/obo/FMA_12516>
PREFIX ada_num: <http://purl.obolibrary.org/obo/OHD_0000065>
PREFIX patient: <http://purl.obolibrary.org/obo/OHD_0000012>
PREFIX occurrence_date: <http://purl.obolibrary.org/obo/OHD_0000015>
PREFIX dentition: <http://purl.obolibrary.org/obo/FMA_75152>
PREFIX missing_tooth_finding: <http://purl.obolibrary.org/obo/OHD_0000026>
PREFIX missing_tooth_num: <http://purl.obolibrary.org/obo/OHD_0000234>
PREFIX is_about: <http://purl.obolibrary.org/obo/IAO_0000136>
PREFIX prop: <http://purl.regenstrief.org/NDPBRN/property/>
PREFIX sesame: <http://www.openrdf.org/schema/sesame#>

#construct # testing
insert 
{
	?tooth_i prop:tooth_loss_date ?loss_date .
} where {
		## find the first date the tooth was lost (either extracted or reported missing)
    select ?tooth_i (min(?date) as ?loss_date) where {
        ?tooth_i a tooth:;
                 prop:ada_tooth_number ?tooth_num .
        {
            ?tooth_i prop:tooth_extraction_date ?date .      # date of extraction
        } union {
            ?tooth_i prop:missing_tooth_finding_date ?date . # date of missing finding
        }
    } group by ?tooth_i
    # limit 100 # testing
}