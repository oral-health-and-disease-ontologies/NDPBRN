## name: insert-missing-tooth-finding-reference.sparql
## author: Bill Duncan
## purpose:
##   This query inserts a relation between a missing tooth finding and the tooth that was
##   found to be missing.
##   This is useful for retrieving previously existing teeth that then went missing.
##   It helps speed up access to this information by simply querying for: 
##     ?finding_i prop:references ?tooth_i

## NB: This query depends on running previously running:
## 	* insert-tooth-instance-tooth-num.sparql
##  * insert-missing-tooth-finding-number.sparql

PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX part_of: <http://purl.obolibrary.org/obo/BFO_0000050>
PREFIX has_part: <http://purl.obolibrary.org/obo/BFO_0000051>
PREFIX tooth: <http://purl.obolibrary.org/obo/FMA_12516>
PREFIX ada_num: <http://purl.obolibrary.org/obo/OHD_0000065>
PREFIX patient: <http://purl.obolibrary.org/obo/OHD_0000012>
PREFIX occurrence_date: <http://purl.obolibrary.org/obo/OHD_0000015>
PREFIX dentition: <http://purl.obolibrary.org/obo/FMA_75152>
PREFIX missing_tooth_finding: <http://purl.obolibrary.org/obo/OHD_0000026>
PREFIX missing_tooth_num: <http://purl.obolibrary.org/obo/OHD_0000234>
PREFIX is_about: <http://purl.obolibrary.org/obo/IAO_0000136>
PREFIX prop: <http://purl.regenstrief.org/NDPBRN/property/>
PREFIX sesame: <http://www.openrdf.org/schema/sesame#>

#construct
insert 
{
	?finding_i prop:references ?tooth_i .
} where {
    ## find patients, teeth, and dentitions
    ?patient_i a patient:;
               has_part: ?tooth_i ;
               has_part: ?dentition_i .
    ?tooth_i a tooth:;
        	 prop:ada_tooth_number ?tooth_num .
    ?dentition_i a dentition: .
    
    ## get the date of the missing tooth finding
    ## the same tooth number as the tooth (i.e., ?tooth_num)
    ?finding_i a missing_tooth_finding:; 
               is_about: ?dentition_i;
               prop:missing_tooth_number ?tooth_num .
    
    filter (!isblank(?patient_i))
    filter (!isblank(?tooth_i))
    filter (!isblank(?finding_i))
    filter (!isblank(?dentition_i))
}

