## name: proc-surface-info.sparql
## author: Bill Duncan
## purpose: 
##	find surfaces restored by procedures
##	prefixes are defined in prefixes.sparql

select distinct
    ?patient_id
    ?tooth_id
    ?tooth_i
    ?tooth_num
    ?proc_i
    ?surface_i
    ?m
    ?o
    ?d
    ?b
    ?f
    ?l
    ?i
where {
    ?patient_i a patient: .
    ?tooth_i a tooth:;
             part_of: ?patient_i;
             sesame:directType ?tooth_t .
    ?tooth_t ada_num: ?ada_num .
    
    ?surface_i a restored_surface:;
               sesame:directType ?surface_t .
    
    ?proc_i a procedure:;
            sesame:directType ?proc_t;
            has_specified_input: ?tooth_i;
            has_specified_output: ?surface_i;
            occur_date: ?occur_date .
    
    filter (!isblank(?patient_i))
    filter (!isblank(?tooth_i))
    filter (!isblank(?proc_i))
    filter (!isblank(?surface_i))
    filter (!isblank(?surface_t))
    
    bind (strafter(str(?patient_i), "patient/") as ?patient_id)
    bind (strafter(str(?tooth_i), "tooth/") as ?tooth_id)
    bind (strafter(str(?proc_i), "procedure/") as ?proc_id)
    bind (strafter(?ada_num, "Tooth ") as ?tooth_num)
    bind(if(?surface_t = restored_buccal:, 1, 0) as ?b)
    bind(if(?surface_t = restored_distal:, 1, 0) as ?d)
    bind(if(?surface_t = restored_mesial:, 1, 0) as ?m)
    bind(if(?surface_t = restored_labial:, 1, 0) as ?f)
    bind(if(?surface_t = restored_lingual:, 1, 0) as ?l)
    bind(if(?surface_t = restored_incisal:, 1, 0) as ?i)
    bind(if(?surface_t = restored_occlusal:, 1, 0) as ?o)
    
} 

