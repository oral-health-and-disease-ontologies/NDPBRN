## name: insert-missing-tooth-finding-date.sparql
## author: Bill Duncan
## purpose:
## This query inserts the date on which a given tooth was found to be missing
## This is useful for retrieving missing date of a tooth.
## It helps speed up access to this information by simply querying for: 
##   ?tooth_i prop:missing_tooth_finding_date ?missing_date .

PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX part_of: <http://purl.obolibrary.org/obo/BFO_0000050>
PREFIX tooth: <http://purl.obolibrary.org/obo/FMA_12516>
PREFIX ada_num: <http://purl.obolibrary.org/obo/OHD_0000065>
PREFIX patient: <http://purl.obolibrary.org/obo/OHD_0000012>
PREFIX occurrence_date: <http://purl.obolibrary.org/obo/OHD_0000015>
PREFIX dentition: <http://purl.obolibrary.org/obo/FMA_75152>
PREFIX missing_tooth_finding: <http://purl.obolibrary.org/obo/OHD_0000026>
PREFIX missing_tooth_num: <http://purl.obolibrary.org/obo/OHD_0000234>
PREFIX is_about: <http://purl.obolibrary.org/obo/IAO_0000136>
PREFIX prop: <http://purl.regenstrief.org/NDPBRN/property/>
PREFIX sesame: <http://www.openrdf.org/schema/sesame#>

#construct
insert 
{
	?tooth_i prop:missing_tooth_finding_date ?missing_date .
} where {
    ## find instance of patient and tooth
    ?patient_i a patient: .
    
    ?tooth_i sesame:directType ?tooth_t; 
             part_of: ?patient_i .
    ?tooth_t ada_num: ?ada_num .

    ## find patients dentitions
    ?dentition_i a dentition:;
                 missing_tooth_num: ?missing_num;
                 part_of: ?patient_i .
    
    
    ## find missing tooth findings about the dentition
    ?event_t rdfs:subClassOf missing_tooth_finding:; 
             rdfs:label ?event_name .
             
    ?event_i a ?event_t; 
             is_about: ?dentition_i;
             occurrence_date: ?missing_date .
    
    ## match ADA tooth num to missing tooth num
    filter (strafter(str(?ada_num), "Tooth ") = str(?missing_num))
}

