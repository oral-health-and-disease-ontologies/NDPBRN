## name: insert-missing-tooth-finding-date.sparql
## author: Bill Duncan
## purpose:
## This query inserts the date on which a given tooth was found to be missing
## This is useful for retrieving missing date of a tooth.
## It helps speed up access to this information by simply querying for: 
##   ?tooth_i prop:missing_tooth_finding_date ?missing_date

PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX part_of: <http://purl.obolibrary.org/obo/BFO_0000050>
PREFIX has_part: <http://purl.obolibrary.org/obo/BFO_0000051>
PREFIX tooth: <http://purl.obolibrary.org/obo/FMA_12516>
PREFIX ada_num: <http://purl.obolibrary.org/obo/OHD_0000065>
PREFIX patient: <http://purl.obolibrary.org/obo/OHD_0000012>
PREFIX occurrence_date: <http://purl.obolibrary.org/obo/OHD_0000015>
PREFIX dentition: <http://purl.obolibrary.org/obo/FMA_75152>
PREFIX missing_tooth_finding: <http://purl.obolibrary.org/obo/OHD_0000026>
PREFIX missing_tooth_num: <http://purl.obolibrary.org/obo/OHD_0000234>
PREFIX is_about: <http://purl.obolibrary.org/obo/IAO_0000136>
PREFIX prop: <http://purl.regenstrief.org/NDPBRN/property/>
PREFIX sesame: <http://www.openrdf.org/schema/sesame#>

#construct
insert 
{
	?tooth_i prop:missing_tooth_finding_date ?missing_date .
} where {
    ## find patients and teeth
    ?patient_i a patient:;
               has_part: ?tooth_i .
    
    ## find dentition's missing tooth numbers
    ?dentition_i a dentition:;
                 part_of: ?patient_i;
                 missing_tooth_num: ?tooth_num .
    
    ## get the date of the missing tooth finding
    ?finding_i a missing_tooth_finding:; 
               is_about: ?dentition_i;
               occurrence_date: ?missing_date .
    
    ## find teeth teeth that are part of the patient
    ## and have a tooth number that matches the dentition's missint tooth number
    ?tooth_i prop:ada_tooth_number ?missing_num;
    		 sesame:directType ?tooth_t . # for some reason this speeds the query up
             
    filter (!isblank(?patient_i))
    filter (!isblank(?dentition_i))
    filter (!isblank(?finding_i))
}


