BASE <http://purl.regenstrief.org/NDPBRN/dental-practice/>
PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX resin_filling_proc: <http://purl.obolibrary.org/obo/OHD_0000042>
PREFIX root_canal_treatment: <http://purl.obolibrary.org/obo/OHD_0000230>
PREFIX part_of: <http://purl.obolibrary.org/obo/BFO_0000050>
PREFIX tooth: <http://purl.obolibrary.org/obo/FMA_12516>
PREFIX posterior_tooth: <http://purl.obolibrary.org/obo/OHD_0000308>
PREFIX has_input: <http://purl.obolibrary.org/obo/OBI_0000293>
PREFIX patient: <http://purl.obolibrary.org/obo/OHD_0000012>
PREFIX occurrence_date: <http://purl.obolibrary.org/obo/OHD_0000015>
PREFIX prop: <http://purl.regenstrief.org/NDPBRN/property/>
PREFIX pbrn_id: <http://purl.obolibrary.org/obo/OHD_0000273>
PREFIX sesame: <http://www.openrdf.org/schema/sesame#>

insert {
    ?tooth_pcr_i prop:first_PCR_date ?first_PCR .
    ?tooth_rct_i prop:first_RCT_date ?first_RCT .
}
where {
    {
        ## find the first PCR on tooth
        select ?patient_i ?tooth_pcr_i (min(?pcr_date) as ?first_PCR)  where {
            ## find instance of patient and posterior tooth
            ?patient_i a patient:;
               		   pbrn_id: ?pbrn_id .
            ?tooth_t rdfs:subClassOf posterior_tooth: .
            ?tooth_pcr_i sesame:directType ?tooth_t;
                     part_of: ?patient_i .
            
            ?pcr_proc_i sesame:directType resin_filling_proc:;
                        has_input: ?patient_i;
                        has_input: ?tooth_pcr_i;
                        occurrence_date: ?pcr_date .
#            filter(str(?pbrn_id) = "NDPBRN practice B_15") # testing
        } group by ?patient_i ?tooth_pcr_i    
    } union {    
        ## find the first RCT on tooth
        select ?patient_i ?tooth_rct_i (min(?rct_date) as ?first_RCT)  where {
            ## find instance of patient and tooth
            ?patient_i a patient:;
               		   pbrn_id: ?pbrn_id .
            ?tooth_t rdfs:subClassOf tooth: .
            ?tooth_rct_i sesame:directType ?tooth_t;
                     part_of: ?patient_i .
            
            ?rct_proc_i sesame:directType root_canal_treatment:;
                        has_input: ?patient_i;
                        has_input: ?tooth_rct_i;
                        occurrence_date: ?rct_date .
#            filter(str(?pbrn_id) = "NDPBRN practice B_15") # testing
        } group by ?patient_i ?tooth_rct_i
    }
}
#limit 100 # testing