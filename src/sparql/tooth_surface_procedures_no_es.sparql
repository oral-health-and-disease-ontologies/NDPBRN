BASE <http://purl.regenstrief.org/NDPBRN/dental-practice/>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX owl: <http://www.w3.org/2002/07/owl#>
PREFIX obo: <http://purl.obolibrary.org/obo/>
PREFIX pbrn_id: <http://purl.obolibrary.org/obo/OHD_0000273>
PREFIX sesame: <http://www.openrdf.org/schema/sesame#>
PREFIX patient: <http://purl.obolibrary.org/obo/OHD_0000012>
PREFIX tooth: <http://purl.obolibrary.org/obo/FMA_12516>
PREFIX restored_surface: <http://purl.obolibrary.org/obo/OHD_0000208>
PREFIX restored_mesial: <http://purl.obolibrary.org/obo/OHD_0000227>
PREFIX restored_occlusal: <http://purl.obolibrary.org/obo/OHD_0000228>
PREFIX restored_distal: <http://purl.obolibrary.org/obo/OHD_0000223>
PREFIX restored_buccal: <http://purl.obolibrary.org/obo/OHD_0000222>
PREFIX restored_labial: <http://purl.obolibrary.org/obo/OHD_0000225>
PREFIX restored_lingual: <http://purl.obolibrary.org/obo/OHD_0000226>
PREFIX restored_incisal: <http://purl.obolibrary.org/obo/OHD_0000224>
PREFIX dental_procedure: <http://purl.obolibrary.org/obo/OHD_0000002>
PREFIX extraction_procedure: <http://purl.obolibrary.org/obo/OHD_0000057>
PREFIX procedure_finding: <http://purl.obolibrary.org/obo/OHD_0000221>
PREFIX ada_num: <http://purl.obolibrary.org/obo/OHD_0000065>
PREFIX part_of: <http://purl.obolibrary.org/obo/BFO_0000050>
PREFIX  has_output: <http://purl.obolibrary.org/obo/OBI_0000299>
PREFIX  occurrence_date: <http://purl.obolibrary.org/obo/OHD_0000015>
PREFIX is_about: <http://purl.obolibrary.org/obo/IAO_0000136>
PREFIX prop: <http://purl.regenstrief.org/NDPBRN/property/>

select distinct ?event_id ?tooth_id ?tooth_num ?m ?o ?d ?b ?l ?f ?i
where {
    ## find instance of patient and patient's DOB
    ?patient_i 
        a patient:;
        pbrn_id: ?pbrn_id .
    
    ## find patients' teeth
    ?tooth_i
        a tooth:;
        prop:ada_tooth_number ?tooth_num;
        part_of: ?patient_i .
    
    ## find restored surface that are part of tooth
#    ?surface_i
#        a restored_surface:;
#        part_of: ?tooth_i;
#        prop:surface_letter ?surface .
    
    # find dental procedures performed on the tooth's surface
    ?event_i 
		a dental_procedure:;
	    has_output: [a restored_surface:; part_of: ?tooth_i] .
#        has_output: ?surface_i .
    
    ## find which surfaces were restored by the procedure
    optional {
        ?event_i 
            has_output: ?mesial . # mesial
        ?mesial 
            sesame:directType restored_mesial:;
            prop:surface_letter ?surface_m;
            part_of: ?tooth_i .
    } 
    optional {
        ?event_i 
            has_output: ?occlusal . #occlusal
        ?occlusal 
            sesame:directType restored_occlusal:; 
            prop:surface_letter ?surface_o;
            part_of: ?tooth_i . 
    } 
    optional {
        ?event_i 
            has_output: ?distal . # distal
        ?distal 
            sesame:directType restored_distal:; 
            prop:surface_letter ?surface_d;
            part_of: ?tooth_i .
    } 
    optional {
        ?event_i 
            has_output: ?buccal . # buccal
        ?buccal 
            sesame:directType restored_buccal:; 
            prop:surface_letter ?surface_b;
            part_of: ?tooth_i .
    } 
    optional {
        ?event_i 
            has_output: ?lingual . # lingual
        ?lingual
            sesame:directType restored_lingual:; 
            prop:surface_letter ?surface_l;
            part_of: ?tooth_i .
    } 
    optional {
        ?event_i 
            has_output: ?facial . # facial/labial
        ?facial 
            sesame:directType restored_labial:; 
            prop:surface_letter ?surface_f;
            part_of: ?tooth_i .
    } 
    optional {
        ?event_i 
            has_output: ?incisal . # incisal
        ?incisal 
            sesame:directType restored_incisal:; 
            prop:surface_letter ?surface_incisal;
            part_of: ?tooth_i .
    }
 
    # remove procedures that are existing services
    filter not exists {
        ?procedure_finding 
            a procedure_finding:;
            is_about: ?event_i .
    }
    
    bind(strafter(str(?pbrn_id), "NDPBRN practice ") as ?practice)
    bind(strafter(str(?patient_i), "patient/") as ?patient_id)
    bind(strafter(str(?tooth_i), "tooth/") as ?tooth_id)
    bind(strafter(str(?event_i), "http://purl.regenstrief.org/NDPBRN/dental-practice/") as ?event_id)
    
    # flags for surfaces
    bind(if(bound(?surface_m), 1, 0) as ?m) # mesial
    bind(if(bound(?occlusal), 1, 0) as ?o) # occlusal
    bind(if(bound(?distal), 1, 0) as ?d) # distal
    bind(if(bound(?buccal), 1, 0) as ?b) # buccal
    bind(if(bound(?lingual), 1, 0) as ?l) # lingual
    bind(if(bound(?facial), 1, 0) as ?f) # facial
    bind(if(bound(?incisal), 1, 0) as ?i) # incisal
}
# limit 100